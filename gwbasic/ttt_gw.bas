5 DEFINT A-Z
10 DIM B(3,3)
11 LET B(0,0) = 1
12 LET AL = -100
13 LET BE = 100
14 LET MC = 0
18 GOSUB 1000
20 GOSUB 2000
25 PRINT "winner";WINNER
30 GOSUB 3000
35 PRINT "cats";CATS
40 GOSUB 4000
50 PRINT "score";SCORE
60 PRINT "move count";MC
100 END
1000 FOR RP = 0 TO 2
1010 PRINT B(RP,0);" ";B(RP,1);" ";B(RP,2)
1020 NEXT RP
1025 RETURN
2000 LET WINNER = 0
2003 FOR RW = 0 TO 2
2005 LET P = B(RW,0)
2010 IF P <> 0 AND P = B(RW,1) AND P = B(RW,2) THEN WINNER = P : RETURN
2015 NEXT RW
2020 FOR CW = 0 TO 2
2025 LET P = B(0,CW)
2030 IF P <> 0 AND P = B(1,CW) AND P = B(2,CW) THEN WINNER = P : RETURN
2035 NEXT CW
2040 LET P = B(0,0)
2045 IF P <> 0 AND P = B(1,1) AND P = B(2,2) THEN WINNER = P : RETURN
2050 LET P = B(0,2)
2055 IF P <> 0 AND P = B(1,1) AND P = B(2,0) THEN WINNER = P : RETURN
2070 RETURN
3000 LET CATS = 0
3005 FOR RC = 0 TO 2
3010 FOR CC = 0 TO 2
3015 IF 0 = B(RC,CC) THEN RETURN
3020 NEXT CC
3025 NEXT RC
3030 LET CATS = 1
3035 RETURN
4000 REM minmax
4010 DIM MSR%(10)
4020 DIM MSC%(10)
4022 DIM MSV%(10)
4023 DIM MSM%(10)
4024 DIM SA%(10)
4025 DIM SB%(10)
4030 LET STACK = 0
4040 LET V = 0
4050 LET MAXIMIZE = 0
4060 LET SCORE = 0
4100 LET MC = MC + 1
4102 IF STACK < 4 THEN GOTO 4150 
4105 GOSUB 2000
4110 IF WINNER = 1 THEN SCORE = 1 : GOTO 4280
4120 IF WINNER = 2 THEN SCORE = -1 : GOTO 4280
4140 IF STACK = 8 THEN SCORE = 0 : GOTO 4280
4150 IF MAXIMIZE = 1 THEN V = -100 ELSE V = 100
4160 LET R = 0
4170 LET C = 0
4180 IF 0 <> B(R,C) THEN GOTO 4500
4200 IF MAXIMIZE = 1 THEN B(R,C) = 1 ELSE B(R,C) = 2
4210 LET MSR(STACK) = R
4220 LET MSC(STACK) = C
4230 LET MSV(STACK) = V
4240 LET MSM(STACK) = MAXIMIZE
4245 LET SA(STACK ) = AL
4246 LET SB(STACK) = BE
4250 IF 0 = MAXIMIZE THEN MAXIMIZE = 1 ELSE MAXIMIZE = 0
4260 LET STACK = STACK + 1
4270 GOTO 4100
4280 LET STACK = STACK - 1
4290 LET R = MSR(STACK)
4300 LET C = MSC(STACK)
4310 LET V = MSV(STACK)
4320 LET MAXIMIZE = MSM(STACK)
4325 LET AL = SA(STACK)
4326 LET BE = SB(STACK)
4328 LET B(R,C) = 0
4330 IF MAXIMIZE = 1 AND SCORE > V THEN V = SCORE
4335 IF MAXIMIZE = 1 AND V > AL THEN AL = V
4336 IF MAXIMIZE = 1 AND AL >= BE THEN C = 2 : R = 2
4340 IF MAXIMIZE = 0 AND SCORE < V THEN V = SCORE
4350 IF MAXIMIZE = 0 AND V < BE THEN BE = V
4360 IF MAXIMIZE = 0 AND BE <= AL THEN C = 2 : R = 2
4500 LET C = C + 1
4501 IF 0 = STACK THEN PRINT "c";C
4505 IF C < 3 THEN GOTO 4180
4510 LET R = R + 1
4511 IF 0 = STACK THEN PRINT "r";R
4515 IF R < 3 THEN GOTO 4170
4520 LET SCORE = V
4530 IF STACK = 0 THEN RETURN
4540 GOTO 4280
